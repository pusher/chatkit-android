plugins {
    id 'kotlin'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.gitlab.arturbosch.detekt' version '1.5.1'
    id 'org.jlleitschuh.gradle.ktlint' version '9.1.1'
    id 'jacoco'
}

apply plugin: 'org.jetbrains.dokka'

dependencies {

    if (rootProject.properties.containsKey('pusher_platform_local')) {
        api project(':pusher-platform-core')
    } else {
        api "com.pusher:pusher-platform-core:$pusher_platform_version"
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.reduxkotlin:redux-kotlin:0.3.1"
    api 'com.squareup.okhttp3:okhttp:3.12.6'
    implementation 'com.google.code.gson:gson:2.8.5'

    testImplementation('com.willowtreeapps.assertk:assertk:0.21'){
        because("we want to use assertk for all tests going forward")
    }
    testImplementation("io.mockk:mockk:1.9.3") {
        because("mockk is a pure kotlin mocking framework we want to use going forward")
    }
    testImplementation('com.google.truth:truth:0.40'){
        because("google truth is used for existing v1 tests")
    }
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "org.mockito:mockito-inline:$mockito_version"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    testImplementation 'org.junit.platform:junit-platform-runner:1.6.0'
    testImplementation "com.auth0:java-jwt:3.3.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"

}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

sourceSets.test {
    java.srcDirs = ['src/test/kotlin', 'src/integrationTest/kotlin']
    resources.srcDirs = ['src/test/resources', 'src/integrationTest/resources']
}

afterEvaluate {
    // adds properties for tests
    def testTask = tasks.getByName('test')
    testTask.systemProperties += rootProject.properties.findAll { it.value instanceof String }
    testTask.systemProperties += System.env
}

bintray {
    user = bintray_user
    key = bintray_api_key
    publish = true
    override = true
    publications = ['Release']
    pkg {
        repo = 'maven'
        name = "$POM_ARTIFACT_ID"
        userOrg = "$POM_DEVELOPER_NAME"
        licenses = ["$POM_LICENCE_NAME"]
        vcsUrl = "$POM_SCM_URL"
        version {
            name = "$VERSION_NAME"
            desc = "$POM_DESCRIPTION"
            released  = new Date()
            vcsTag = "$VERSION_NAME"
            gpg {
                sign = true
                passphrase = System.getenv('SONATYPE_GPG_PASSPHRASE')
            }
        }
    }
}

bintrayUpload.dependsOn(['checkIfPusherPlatformLocalIsSet', 'verifyBintrayCredentials'])

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url "$POM_LICENCE_URL"
            distribution 'repo'
        }
    }
    developers {
        developer {
            name 'Pusher Limited'
            email 'support@pusher.com'
        }
    }

    scm {
        url "$POM_SCM_URL"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        Release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId "$POM_ARTIFACT_ID"
            groupId "$GROUP"
            version "$VERSION_NAME"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', "$POM_DESCRIPTION")
                root.appendNode('name', "$POM_NAME")
                root.appendNode('url', "$POM_URL")
                root.children().last() + pomConfig
            }
        }
    }
}

detekt {
    baseline = file("$projectDir/config/baseline.xml")

    reports {
        html.enabled = true
    }
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports.xml.enabled = true
    reports.html.enabled = false
    reports.csv.enabled = false
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}
plugins {
    id 'kotlin'
    id 'java-library'
    id 'org.junit.platform.gradle.plugin'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.7.3"
}

apply from: "$projectDir/../gradle/jacoco.gradle"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    api "com.pusher:pusher-platform-core:$pusher_platform_version"

    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "com.google.truth:truth:0.40"
    testImplementation 'org.jetbrains.spek:spek-api:1.1.5'
    testImplementation 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
    testImplementation 'org.junit.platform:junit-platform-runner:1.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.3'
    testImplementation "com.auth0:java-jwt:3.3.0"

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.0.3'
}

test {
    useJUnitPlatform()
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

sourceSets.test {
    java.srcDirs = ['src/test/kotlin', 'src/integrationTest/kotlin']
    resources.srcDirs = ['src/test/resources', 'src/integrationTest/resources']
}

junitPlatform {
    details 'tree'
    filters {
        engines {
            include 'junit-jupiter', 'spek'
        }
    }
}

afterEvaluate {
    // adds properties for tests
    def junitPlatformTestTask = tasks.getByName('junitPlatformTest')
    junitPlatformTestTask.systemProperties += rootProject.properties.findAll { it.value instanceof String }
    junitPlatformTestTask.systemProperties += System.env
}

bintray {
    user = bintray_user
    key = bintry_api_key
    publish = true
    override = true
    publications = ['Release']
    pkg {
        repo = 'maven'
        name = "$POM_ARTIFACT_ID"
        userOrg = "$POM_DEVELOPER_ID"
        licenses = ["$POM_LICENCE_NAME"]
        vcsUrl = "$POM_SCM_URL"
        version {
            name = "$VERSION_NAME"
            desc = "$POM_DESCRIPTION"
            released  = new Date()
            vcsTag = "$VERSION_NAME"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// Create the publication with the pom configuration:
publishing {
    publications {
        Release(MavenPublication) {
            artifactId "$POM_ARTIFACT_ID"
            from components.java
            groupId "$GROUP"
            version "$VERSION_NAME"
            artifact sourcesJar
        }
    }
}
